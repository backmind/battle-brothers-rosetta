// Hooking strategies here - names and such intercept close to their origin, because there are code
// out there both in vanilla and mods, which concats and wraps those a lot. The rest are intercepted
// at squirrel - js border mostly.
local def = ::Rosetta, mod = def.mh, _ = def._;
local Table = ::std.Table, Str = ::std.Str;

local simpleGetter = @(__original) function () {
    return _(__original());
}
local function makeGetter(_field) {
    return @(__original) function () {
        local script = IO.scriptFilenameByHash(this.ClassNameHash);
        return _(__original(), script + "." + _field);
    }
}

mod.queue("<mod_msu", function () {
    mod.hook("scripts/items/item", function (q) {
        // q.getName = simpleGetter;
        q.getDescription = makeGetter("Description");
    })
    mod.hook("scripts/skills/skill", function (q) {
        // q.getName = simpleGetter;
        q.getDescription = makeGetter("Description");
    })

    // Need to hook earlier because if Nested Tooltips messing these
    mod.hookTree("scripts/entity/tactical/actor", function (q) {
        q.getNameOnly = simpleGetter;
        q.getKilledName = simpleGetter;
        q.getTitle = simpleGetter;
        q.getName = @(__original) function () {
            local ret = __original();
            // Allow translating name and title separately
            local vanilla = m.Title == "" ? m.Name : m.Name + " " + m.Title;
            if (ret == vanilla) return m.Title == "" ? _(m.Name) : _(m.Name) + " " + _(m.Title);
            return _(ret);
        }
    })
})

mod.queue(">mod_msu", function () {
    def.msu <- ::MSU.Class.Mod(def.ID, def.Version, def.Name);

    local msd = ::MSU.System.Registry.ModSourceDomain, upd = def.Updates;
    def.msu.Registry.addModSource(msd.NexusMods, upd.nexus);
    def.msu.Registry.addModSource(msd.GitHub, upd.github);

    // This fixes MSU.isWeaponType() for russian language, which fixes some skills dependent on it,
    // i.e. many Reforged perks.
    // TODO: update for the newest MSU
    Table.extend(::Const.Items.WeaponType, {
        "Топор": 1
        "Лук": 2
        "Тесак": 4
        "Арбалет": 8
        "Кинжал": 16
        "Пищаль": 32
        "Кистень": 64
        "Молот": 128
        "Булава": 256
        "Древковое оружие": 512
        "Праща": 1024
        "Копьё": 2048
        "Меч": 4096
        "Посох": 8192
        "Метательное оружие": 16384
        "Музыкальный инструмент": 32768
    })

    // New item types, i.e. Artifact
    local Items_getItemTypeName = ::Const.Items.getItemTypeName;
    ::Const.Items.getItemTypeName = function (_itemType) {
        return _(Items_getItemTypeName(_itemType))
    }

    // Hooks
    mod.hook("scripts/ui/screens/tactical/modules/topbar/tactical_screen_topbar_event_log",
            function (q) {
        q.log = q.logEx = @(__original) function (_text) {
            __original(_(_text))
        }
    })

    // Popup dialogs (single chokepoint for tactical/world/campfire showDialogPopup)
    mod.hook("scripts/ui/screens/dialog_screen", function (q) {
        q.show = @(__original) function (_title, _text, _doneCallback, _okCallback = null, _cancelCallback = null, _isMonologue = false) {
            return __original(_(_title), _(_text), _doneCallback, _okCallback, _cancelCallback, _isMonologue);
        }
    })

    // Combat result screen: title matches literal pairs (Victory/Defeat/Retreat),
    // subtitle matches the rounds patterns
    mod.hook("scripts/ui/screens/tactical/tactical_combat_result_screen", function (q) {
        q.onQueryCombatInformation = @(__original) function () {
            local ret = __original();
            if (ret != null) {
                if ("title" in ret) ret.title = _(ret.title);
                if ("subTitle" in ret) ret.subTitle = _(ret.subTitle);
            }
            return ret;
        }
    })

    // Loading screen tips
    mod.hook("scripts/ui/screens/loading/loading_screen", function (q) {
        q.onQueryData = @(__original) function () {
            local ret = __original();
            if (ret != null && "text" in ret) ret.text = _(ret.text);
            return ret;
        }
    })

    // World map party labels ("Peasants (4)"): the engine draws the label but the text
    // comes from updateStrength(), which concats getName() + " (" + n + ")", so the
    // suffix stays outside the literal and no pattern is needed. Plain hook, not
    // hookTree: the only subclass redefining getName (attached_location) delegates
    // to the base in its live branch, so this point already covers it.
    mod.hook("scripts/entity/world/world_entity", function (q) {
        q.getName = simpleGetter;
    })

    // Perks
    mod.hook("scripts/ui/global/data_helper", function (q) {
        q.convertEntityToUIData = @(__original) function (_entity, _activeEntity) {
            local result = __original(_entity, _activeEntity);
            if ("necro_perkTree" in result) {
                result.necro_perkTree = def.translatePerkTree(result.necro_perkTree);
            }
            return result;
        }
    })

    local Perks_findById = ::Const.Perks.findById;
    ::Const.Perks.findById = function (_id) {
        return def.translatePerk(Perks_findById(_id));
    }

    local tooltipHook = @(__original) function (...) {
        vargv.insert(0, this);
        return def.translateTooltip(__original.acall(vargv));
    }

    // Tooltips
    mod.hook("scripts/ui/screens/tooltip/tooltip_events", function (q) {
        q.onQueryTileTooltipData = tooltipHook;
        q.onQueryEntityTooltipData = tooltipHook;
        q.onQueryEntityTooltipData = tooltipHook;
        q.onQueryRosterEntityTooltipData = tooltipHook;
        q.onQuerySkillTooltipData = tooltipHook;
        q.onQueryStatusEffectTooltipData = tooltipHook;
        q.onQuerySettlementStatusEffectTooltipData = tooltipHook;
        q.onQueryUIElementTooltipData = tooltipHook;
        q.onQueryUIItemTooltipData = tooltipHook;
        q.onQueryUIPerkTooltipData = tooltipHook;
        q.onQueryFollowerTooltipData = tooltipHook;
        if (q.contains("onQueryMSUTooltipData")) q.onQueryMSUTooltipData = tooltipHook;
    })

    // Background
    mod.hook("scripts/skills/backgrounds/character_background", function (q) {
        q.getName = @(__original) function () {
            local ret = __original();
            local parts = Str.split(": ", ret, 1);
            if (parts.len() == 2) return parts[0] + ": " + _(parts[1]);
            return ret;
        }
        q.getNameOnly = simpleGetter;
    })
    mod.hookTree("scripts/skills/backgrounds/character_background", function (q) {
        q.onBuildDescription = @(__original) function () {
            local script = IO.scriptFilenameByHash(this.ClassNameHash);
            return _(__original(), script + ".onBuildDescription");
        }
    })

    mod.hookTree("scripts/entity/tactical/entity", function (q) {
        if (!q.ClassName == "actor" && !q.contains("actor", true)) q.getName = simpleGetter;
        q.getDescription = makeGetter("Description");
    })
    mod.hookTree("scripts/items/item", function (q) {
        q.getName = simpleGetter;
        q.getDescription = makeGetter("Description");
    })
    mod.hookTree("scripts/skills/skill", function (q) {
        q.getName = simpleGetter;
        q.getDescription = makeGetter("Description");
    })
    mod.hookTree("scripts/scenarios/world/starting_scenario", function (q) {
        q.getName = makeGetter("Name");
        q.getDescription = makeGetter("Description");
    })
    // Event and contract text is hooked at buildText ENTRY, where %placeholders% are
    // still raw as extracted pairs store them; at the sq-js border they are already
    // substituted and no pair would match. One point per class covers event titles,
    // bodies, dialog options, contract bulletpoints and the active contract panel
    // title. Plain hook, not hookTree: no vanilla subclass redefines buildText, and
    // hookTree would wrap every descendant on top of the base, translating twice per
    // call. Internal strings passing through buildText may log NOT FOUND; harmless.
    mod.hook("scripts/events/event", function (q) {
        q.buildText = @(__original) function (_text) {
            return __original(_(_text));
        }
    })
    mod.hook("scripts/contracts/contract", function (q) {
        q.getUITitle = simpleGetter;
        q.getUIButtons = tooltipHook;
        q.buildText = @(__original) function (_text) {
            return __original(_(_text));
        }
    })

    // Translate MSU settings: setting labels, page tab names, panel (mod) names, slider labels
    local function hookGetUIData(_cls, _extra = null) {
        local orig = _cls.getUIData;
        _cls.getUIData <- function (_flags = []) {
            local ret = orig.call(this, _flags);
            ret.name = _(ret.name);
            if (_extra != null) _extra(ret);
            return ret;
        }
    }
    hookGetUIData(::MSU.Class.SettingsElement);
    // Direct subclasses of SettingsElement that don't override getUIData snapshot
    // the original method at class-definition time, so the hook above doesn't reach
    // them. AbstractSetting (and its subclasses) overrides getUIData and calls
    // base.getUIData(), which resolves dynamically — those work via the hook above.
    hookGetUIData(::MSU.Class.SettingsTitle);
    hookGetUIData(::MSU.Class.SettingsPage, function (ret) {
        // TODO: hook slider directly once it lands into MSU
        foreach (setting in ret.settings)
            if ("labels" in setting) setting.labels.apply(_);
    });
    hookGetUIData(::MSU.Class.SettingsPanel);

}, ::Hooks.QueueBucket.Late)

// Unified Perk Descriptions
mod.queue(">mod_upd", "<mod_reforged", function () {
    if (!("UPD" in getroottable())) return

    local UPD_getDescription = ::UPD.getDescription;
    ::UPD.getDescription = function (_info) {
        foreach (key in ["Fluff" "Requirement" "Footer"]) {
            local val = Table.get(_info, key, "");
            if (val != "") _info[key] = _(val);
        }
        if ("Effects" in _info)
            foreach (effect in _info.Effects) effect.Description.apply(_);
        return UPD_getDescription(_info);
    }
})
